import java.nio.file.Files

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/releases/"
        }
    }

    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.1.0'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id "com.github.hierynomus.license" version "0.14.0"
    id 'net.researchgate.release' version '2.6.0'
}
apply plugin: 'aversion-apt'
apply plugin: 'aversion-util'
apply plugin: 'java'

util {
    javaVersion = '1.8'
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

repositories {
    mavenCentral()
    flatDir {
        def libExt = System.properties.'java.home' + "/lib/ext/"
        logger.info("Using $libExt for the extended JRE libraries dependency.")
        dirs libExt
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compileOnly group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'
    compile group: 'org.mapdb', name: 'mapdb', version: '3.0.5'
    compile group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    compile group: 'com.techshroom', name: 'templar', version: '0.1.4'
    compile group: 'com.techshroom', name: 'lettar', version: '0.3.0'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: project.jacksonVersion

    compile group: 'org.apache.velocity', name: 'velocity-engine-core', version: '2.0'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.17'

    addAPTReqWComp group: 'com.google.guava', name: 'guava', version: '24.0-jre'
    addAPT group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc3'
    addAPT group: 'com.google.auto.value', name: 'auto-value', version: '1.5.3'
    addAPTReq name: 'aopalliance'
    addAPTReq name: 'asm'
    addAPTReq name: 'auto-value'
    addAPTReq name: 'auto-common'
    addAPTReq name: 'cglib'
    addAPTReq name: 'commons-collections'
    addAPTReq name: 'commons-lang'
    addAPTReq name: 'dagger'
    addAPTReq name: 'javawriter'
    addAPTReq name: 'javax.inject'
    addAPTReq name: 'velocity'

    // require JavaFX for properties -- it should be available...
    compile name: 'jfxrt'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
    }
    header = rootProject.file('HEADER.txt')
    ignoreFailures = false
    strictCheck = true
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

jar {
    manifest {
        attributes("Main-Class": "com.techshroom.ytmp3.YoutubeMp3Server")
    }
}

assemble.dependsOn(shadowJar)

// Super exciting! We're doing ES6 transpilation!
ext.destDirBase = project.file("${buildDir}/transpiled")
tasks.create("transpileJavascript", TranspileJavascript) { task ->
    sourceDir = file("transpile-source/javascript")
    destinationDir = new File(project.destDirBase, "javascript")
}
tasks.create("transpileScss", TranspileSass) { task ->
    sourceDir = file("transpile-source/scss")
    destinationDir = new File(project.destDirBase, "stylesheets")
}
task transpileResources(dependsOn: [transpileJavascript, transpileScss]);

sourceSets.main {
    output.dir(project.destDirBase, builtBy: 'transpileResources')
}

class TranspileJavascript extends DefaultTask {
    {
        // we should redo when these change
        inputs.files("package.json", ".babelrc")
    }

    @InputDirectory
    File sourceDir

    @OutputDirectory
    File destinationDir

    @TaskAction
    void generateAssets() {
        project.exec {
            executable project.file("./node_modules/babel-cli/bin/babel.js")
            args '-d', destinationDir, sourceDir
        }
    }
}

class TranspileSass extends DefaultTask {
    @InputDirectory
    File sourceDir

    @OutputDirectory
    File destinationDir

    @TaskAction
    void generateAssets() {
        def cssMiddleman = Files.createTempDirectory("yt-mp3-css-middleman").toFile()
        project.exec {
            executable 'sass'
            args '--scss', '--sourcemap=none', '--update', "$sourceDir:$cssMiddleman"
        }
        project.exec {
            executable project.file("./node_modules/postcss-cli/bin/postcss")
            args = ['--use', 'autoprefixer', '--dir', destinationDir] + project.fileTree(cssMiddleman).files
        }
    }
}
